C$$$  MAIN PROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: BUFR1B
C   PRGMMR: KEYSER           ORG: NP22        DATE: 2005-06-21
C
C ABSTRACT: GENERATES BUFR REPORT FROM TOVS/ATOVS 1B DATA AND ENCODES
C   IT INTO OUTPUT BUFR FILE.
C
C PROGRAM HISTORY LOG:
C 2000-09-06  WOOLLEN  -- ORIGINAL CODE
C 2002-02-11  WOOLLEN  -- MODIFICATIONS AND CORRECTIONS TO OUTPUT BUFR
C       DATASET: "SAID" (0-01-007) CORRECTED TO PROPER WMO CODE TABLE
C       VALUE (WAS 14 FOR NOAA-14, ETC.), "SIID" (0-02-019) REPLACED
C       "SIDU" (0-02-021) WHICH DIDN'T SEEM TO BE CORRECT, "HMSL"
C       (0-07-002) CORRECTED TO PROPER UNITS OF METERS (WAS BEING
C       STORED IN KM), "LSQL" (0-08-012) CORRECTED TO PROPER WMO CODE
C       TABLE VALUE (0-LAND/1-SEA) (WAS BACKWARDS), "TMBR" (0-12-163)
C       CORRECTED TO PROPER UNITS OF K (WAS BEING STORED AS
C       K + 273.15), CHANNEL 20 "TMBR" SET TO MISSING FOR HIRS-2 AND
C       HIRS-3 TYPES
C 2004-01-23  KEYSER   -- BASED ON NEW NAMELIST SWITCH "COMPRESS", NOW
C       HAS OPTION TO WRITE COMPRESSED BUFR MESSAGES USING WRITCP
C       INSTEAD OF WRITSB (REMOVES THE NEED FOR THE DOWNSTREAM PROGRAM
C       BUFR_COMPRESS)
C 2005-04-29  KEYSER   -- MODIFIED TO HANDLE PROCESSING OF AMSU-A
C       ANTENNA TEMPERATURE (Ta) REPORTS INTO MESSAGE TYPE NC021123
C 2005-06-21  KEYSER   -- MODIFIED TO HANDLE PROCESSING OF MHS AND
C       HIRS-4 BRIGHTNESS TEMPERATURE (Tb) REPORTS INTO MESSAGE TYPES
C       NC021027 AND NC021028, RESPECTIVELY
C
C USAGE: CALL BUFR1B(LUBFR,SUBSET,NREAL,NCHAN,RDATA,NREP)
C
C   INPUT ARGUMENTS:
C     LUBFR  - LOGICAL UNIT FOR BUFR FILE
C     SUBSET - BUFR MESSAGE TYPE FOR TOVS/ATOVS INSTRUMENT/TEMPERATURE
C              TYPE COMBINATION
C     NREAL  - NUMBER OF TOVS/ATOVS HEADER ELEMENTS
C     NCHAN  - NUMBER OF TOVS/ATOVS CHANNELS
C     RDATA  - REAL ARRAY WITH TOVS/ATOVS HEADER AND CHANNEL DATA
C     NREP   - NUMBER OF BUFR REPORTS WRITTEN PRIOR TO THIS CALL TO
C              BUFR1B
C
C   OUTPUT ARGUMENTS:
C     NREP   - NUMBER OF BUFR REPORTS WRITTEN AFTER TO THIS CALL TO
C              BUFR1B
C
C   SUBPROGRAMS CALLED:
C
C     LIBRARY:
C       BUFRLIB  - OPENMB  WRITSB  WRITCP  UFBSEQ
C
C REMARKS:
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  IBM-SP
C
C$$$

      SUBROUTINE BUFR1B(LUBFR,SUBSET,NREAL,NCHAN,RDATA,NREP)
 
      PARAMETER(NDAT=100)
 
      CHARACTER*8 SUBSET,COMPRESS,PROCESS_Tb,PROCESS_Ta
      REAL*4      RDATA(NREAL+NCHAN)
      REAL*8      BUFRF(NDAT)

      COMMON/SWITCHES/COMPRESS,PROCESS_Tb,PROCESS_Ta

      DATA        BMISS       /10E10/

C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
 
C  ISOLATE THE DATE/TIME OF THIS REPORT
C  ------------------------------------
 
      IYR = RDATA(3)
      IMO = RDATA(4)
      IDY = RDATA(5)
      IHR = RDATA(6)/3600
      IMI = (RDATA(6)-IHR*3600)/60
      ISE = (RDATA(6)-IHR*3600-IMI*60)
 
C  CONVERT THE UNPACKED SATELLITE INDICATOR INTO BUFR VALUE
C   (CODE TABLE 0-01-007)
C  --------------------------------------------------------
 
      ICODE = 0
      ICODE = NINT(RDATA(1))
C     IF(ICODE.EQ.? ) ICODE = 708    ! TIROS-N
C     IF(ICODE.EQ.? ) ICODE = 700    ! TIROS-M
      IF(ICODE.EQ.1 ) ICODE = 701    ! NOAA-1 
      IF(ICODE.EQ.2 ) ICODE = 702    ! NOAA-2
      IF(ICODE.EQ.3 ) ICODE = 703    ! NOAA-3
      IF(ICODE.EQ.4 ) ICODE = 704    ! NOAA-4
      IF(ICODE.EQ.5 ) ICODE = 705    ! NOAA-5
      IF(ICODE.EQ.6 ) ICODE = 706    ! NOAA-6
      IF(ICODE.EQ.7 ) ICODE = 707    ! NOAA-7
      IF(ICODE.EQ.8 ) ICODE = 200    ! NOAA-8
      IF(ICODE.EQ.9 ) ICODE = 201    ! NOAA-9
      IF(ICODE.EQ.10) ICODE = 202    ! NOAA-10
      IF(ICODE.EQ.11) ICODE = 203    ! NOAA-11
      IF(ICODE.EQ.12) ICODE = 204    ! NOAA-12
      IF(ICODE.EQ.14) ICODE = 205    ! NOAA-14
      IF(ICODE.EQ.15) ICODE = 206    ! NOAA-15
      IF(ICODE.EQ.16) ICODE = 207    ! NOAA-16
      IF(ICODE.EQ.17) ICODE = 208    ! NOAA-17
      IF(ICODE.EQ.18) ICODE = 209    ! NOAA-18
      IF(ICODE.EQ.0) THEN
         WRITE(6,*) '** UNKNOWN SATELLITE ID',
     .    (RDATA(III),III=1,14)
         CALL W3TAGE('BUFR_TRANHIRS2')
         CALL ERREXIT(6)
      ENDIF
 
C  DETERMINE THE BUFR SATELLITE INSTRUMENT (CODE TABLE 0-02-019) BASED
C   ON THE BUFR MESSAGE SUBTYPE
C  ------------------------------------------------------------------
 
      JCODE = 0
      IF(SUBSET(7:8).EQ.'21') JCODE = 605   ! hirs-2 (NC021021) (Tb)
      IF(SUBSET(7:8).EQ.'22') JCODE = 623   ! msu    (NC021022) (Tb)
      IF(SUBSET(7:8).EQ.'23') JCODE = 570   ! amsu-a (NC021023) (Tb)
                                            !    or  (NC021123) (Ta)
      IF(SUBSET(7:8).EQ.'24') JCODE = 574   ! amsu-b (NC021024) (Tb)
      IF(SUBSET(7:8).EQ.'25') JCODE = 606   ! hirs-3 (NC021025) (Tb)
      IF(SUBSET(7:8).EQ.'27') JCODE = 203   ! mhs    (NC021027) (Tb)
      IF(SUBSET(7:8).EQ.'28') JCODE = 607   ! hirs-4 (NC021028) (Tb)
      IF(ICODE.EQ.0) THEN
         WRITE(6,*) '** UNKNOWN SATELLITE INSTRUMENT',
     .    (RDATA(III),III=1,14)
         CALL W3TAGE('BUFR_TRANHIRS2')
         CALL ERREXIT(7)
      ENDIF
 
C  FLIP THE SENSE OF THE LAND/SEA FLAG
C  -----------------------------------
 
      IF(NINT(RDATA(7)).EQ.0) LANDSEA = 1
      IF(NINT(RDATA(7)).EQ.1) LANDSEA = 0
 
C  TRANSLATE THE 1B RECORD TO BUFR FORMAT
C  ------------------------------------------------------------
C  NC021023 | YEAR     MNTH     DAYS     HOUR     MINU     SECO
C  NC021023 | CLAT     CLON     SAID     SIID     FOVN     LSQL
C  NC021023 | SAZA     SOZA     HOLS     HMSL     "BRIT"
C  ------------------------------------------------------------
 
      BUFRF( 1) = IYR
      BUFRF( 2) = IMO
      BUFRF( 3) = IDY
      BUFRF( 4) = IHR
      BUFRF( 5) = IMI
      BUFRF( 6) = ISE
      BUFRF( 7) = RDATA(9)
      BUFRF( 8) = RDATA(10)
      BUFRF( 9) = ICODE
      BUFRF(10) = JCODE
      BUFRF(11) = RDATA(8)
      BUFRF(12) = LANDSEA
      BUFRF(13) = RDATA(11)
      BUFRF(14) = RDATA(12)
      BUFRF(15) = RDATA(13)
      BUFRF(16) = RDATA(14)*1000.
 
      DO N=1,NCHAN
      M = (N-1)*2
      BUFRF(17+M) = (N)
      BUFRF(18+M) = RDATA(14+N)
      ENDDO

C  MAKE SURE HIRS-2, HIRS-3 AND HIRS-4 CHN 20 TEMPERATURE IS MISSING
C  -----------------------------------------------------------------

      IF(SUBSET.EQ.'NC021021') BUFRF(56) = BMISS
      IF(SUBSET.EQ.'NC021025') BUFRF(56) = BMISS
      IF(SUBSET.EQ.'NC021028') BUFRF(56) = BMISS
 
C  WRITE THIS ARRAY INTO BUFR
C  --------------------------
 
      IDATE = IYR*1000000+IMO*10000+IDY*100+IHR
      CALL OPENMB(LUBFR,SUBSET,IDATE)
      CALL UFBSEQ(LUBFR,BUFRF,NDAT,1,IRET,SUBSET)
      NREP = NREP + 1
      IF(COMPRESS.EQ.'YES' .OR. COMPRESS.EQ.'yes') THEN
         CALL WRITCP(LUBFR)
      ELSE
         CALL WRITSB(LUBFR)
      ENDIF
 
C  EXIT HERE
C  ---------
 
      RETURN
      END
